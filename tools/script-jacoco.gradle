apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.4"
}

// Need to set this on all Android modules to have accurate test coverage on Robolectric tests
Closure setUpTestOptions = {
    android {
        testOptions {
            unitTests.all {
                jacoco {
                    includeNoLocationClasses = true
                    jacoco.excludes = ['jdk.internal.*']
                }
            }
        }
    }
}
plugins.withId("com.android.application", setUpTestOptions)
plugins.withId("com.android.library", setUpTestOptions)
task jacocoTestReport(type: JacocoReport, dependsOn: ["testDebugUnitTest"]) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def excludedFiles = ['**/R.class', '**/R$*.class', '**/BR.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/databinding/**/*.*', '**/*_ViewBinding.*', '**/*_MembersInjector.*']
    def debugTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug",
            excludes: excludedFiles)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))

    getClassDirectories().setFrom(debugTree)

    getExecutionData().setFrom(fileTree(dir: "${project.buildDir}", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}